<?xml version="1.0"?>
<!--
  ~ Copyright (C) 2011 flashflexpro@gmail.com  All Rights Reserved.  No
  ~   use, copying or distribution of this work may be made except in
  ~   accordance with a valid license agreement from flashflexpro@gmail.com
  ~   This notice must be included on all copies, modifications and
  ~   derivatives of this work.
  ~
  ~   flashflexpro@gmail.com MAKES NO REPRESENTATIONS OR WARRANTIES ABOUT
  ~   THE SUITABILITY OF THE SOFTWARE, EITHER EXPRESS OR IMPLIED,
  ~   INCLUDING BUT NOT LIMITED TO THE IMPLIED WARRANTIES OF
  ~   MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE, OR
  ~   NON-INFRINGEMENT. flashflexpro@gmail.com SHALL NOT BE LIABLE FOR ANY DAMAGES SUFFERED
  ~   BY LICENSEE AS A RESULT OF USING, MODIFYING OR DISTRIBUTING THIS
  ~   SOFTWARE OR ITS DERIVATIVES.
  -->

<utils:MxmlEventDispatcher xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:utils="com.flashflexpro.as3lib.utils.*">
    <mx:Script><![CDATA[
        import com.flashflexpro.as3lib.*;

        import mx.collections.IList;
        import mx.events.PropertyChangeEvent;
        import mx.events.PropertyChangeEventKind;
        import mx.utils.UIDUtil;

        CONFIG::debugging{
            private static const dest2sync:Dictionary = new Dictionary( true );
        }//debug

        public var priority:int = 0;

        private var _enabled:Boolean = true;

        [Bindable(event="enabledChanged")]
        public function get enabled():Boolean{
            return _enabled;
        }

        public function set enabled( value:Boolean ):void{
            if( value != _enabled ){
                _enabled = value;

                CONFIG::debugging{
                    if( _source != null && _destination != null && _source.length != _destination.length ){
                        throw new Error( this + ".enabled error   " );
                    }//debug
                }
                dispatchEvent( new Event( "enabledChanged" ) );
            }
        }

        private var _synchronizingChange:Boolean = false;
        [Bindable(event="synchronizingChangeChanged")]
        public function get synchronizingChange():Boolean{
            return _synchronizingChange;
        }

        private function setSynchronizingChangeChanged( s:Boolean ):void{
            _synchronizingChange = s;
            dispatchEvent( new Event( "synchronizingChangeChanged" ) );
        }

        private var _source:CrMap;

        [Bindable(event="sourceChanged")]
        public function get source():CrMap{
            return _source;
        }

        public function set source( value:CrMap ):void{
            if( value != _source ){
                if( _source != null ){
                    _source.removeEventListener( PropertyChangeEvent.PROPERTY_CHANGE, onSrcPropertyChanged );
                }
                _source = value;
                initializeSync();
                dispatchEvent( new Event( "sourceChanged" ) );
            }
        }

        private var _destination:IList;
        [Bindable(event="destinationChanged")]
        public function get destination():IList{
            return _destination;
        }

        public function set destination( value:IList ):void{
            if( value != _destination ){
                CONFIG::debugging{
                    if( _destination != null ){
                        if( dest2sync[_destination] == this ){
                            delete dest2sync[_destination];
                        }
                        else{
                            throw new Error( getUIDs() + ".destination error who is pointing at this dest?" )
                        }
                    }
                    if( value != null ){
                        var tmp:* = dest2sync[value];
                        if( tmp != null && tmp != this ){
                            throw new Error( getUIDs() + ".destination error tmp is already pointing to this dest " )
                        }
                        dest2sync[value] = this;
                    }
                }//debug

                _destination = value;

                initializeSync();

                dispatchEvent( new Event( "destinationChanged" ) );
            }
        }

        private var _getDestInst:Function;

        [Bindable(event="getDestInstChanged")]
        public function get getDestInst():Function{
            return _getDestInst;
        }

        /**
         * Plz do a parameter verification at the beginning of the function
         * @param value
         */
        public function set getDestInst( value:Function ):void{
            if( value != _getDestInst ){
                _getDestInst = value;
                CONFIG::debugging{
                    if( _getDestInst == null ){
                        throw new Error( getUIDs() + ".getDestInst error source to destination function can't be null!!!!" );
                    }//debug
                }
                initializeSync();
                dispatchEvent( new Event( "getDestInstChanged" ) );
            }
        }

        private function exeGetDestInst( srcKey:* ):*{
            CONFIG::debugging{
                try{
                    return _getDestInst( srcKey );
                }
                catch( e:Error ){
                    CC.log( getUIDs() + ".exeGetDestInst error :" + e.getStackTrace(), 2, true );
                }
            }
            return _getDestInst( srcKey );
        }

        private var _compare2Insts:Function;
        [Bindable(event="compare2InstsChanged")]
        public function get compare2Insts():Function{
            return _compare2Insts;
        }

        public function set compare2Insts( value:Function ):void{
            if( value != _compare2Insts ){
                _compare2Insts = value;
                if( _compare2Insts != null ){
                    initializeSync();
                }
                dispatchEvent( new Event( "compare2InstsChanged" ) );
            }
        }

        private function exeCompare( target:* ):uint{
            if( _compare2Insts == null || _destination.length == 0 ){
                return _destination.length;
            }

            return findBySplit( target, 0, _destination.length - 1 );
        }

        private function findBySplit( target:*, fromIndex:uint, toIndex:uint ):int{
            var mid:uint = Math.round( ( fromIndex + toIndex ) / 2 );

            var result:int = _compare2Insts.apply( null, [target, _destination.getItemAt( mid )] );
            if( result == 0 ){
                return mid;
            }
            if( fromIndex == toIndex ){
                return result > 0 ? fromIndex + 1 : fromIndex;
            }
            return result > 0 ? findBySplit( target, mid, toIndex ) : findBySplit( target, fromIndex, mid );
        }


        private var _destroyInst:Function;
        /**
         * Plz do a parameter verification at the beginning of the function
         */
        [Bindable(event="destroyInstChanged")]
        public function get destroyInst():Function{
            return _destroyInst;
        }

        public function set destroyInst( value:Function ):void{
            if( value != _destroyInst ){
                _destroyInst = value;
                dispatchEvent( new Event( "destroyInstChanged" ) );
            }
        }

        private function exeDestroy( r:* ):void{
            if( _destroyInst != null ){
                try{
                    _destroyInst.apply( null, [r] );
                }
                catch( e:Error ){
                    CONFIG::debugging{
                        CC.log( getUIDs() + ".exeDestroy error when destroying dest:" + e.getStackTrace(), 2, true )
                    }//debug
                }
            }
        }

        //array of property names
        public var propertiesReplaceOnUpdate:Array;

        protected function onSrcPropertyChanged( event:PropertyChangeEvent ):void{
            if( source == null || destination == null || _getDestInst == null || ! _enabled ){
                return;
            }

            CONFIG::debugging{
                if( _printLog ){
                    CC.log( getUIDs() + ".onSrcChange: event:" + event );
                }
                if( _syncInitializeStatus != 2 ){
                    throw new Error( getUIDs() + ".onSrcLstCollectionChange not initialized!!!" );
                }
            }//debug

            setSynchronizingChangeChanged( true );

            if( event.kind == PropertyChangeEventKind.UPDATE || event.kind == PropertyChangingEvent.PROPERTY_CHANGE_KIND_ADDED ){
                if( event.oldValue != null ){
                    exeDestroy( destination.removeItemAt( _mapVal2LstIndex[event.oldValue] ) );
                    delete  _mapVal2LstIndex[event.oldValue];
                }

                var valueAddingToList:* = exeGetDestInst( event.property );
                var index:uint = exeCompare( valueAddingToList );
                _mapVal2LstIndex[valueAddingToList] = index;
                destination.addItemAt( valueAddingToList, index )
            }
            else if( event.kind == PropertyChangeEventKind.DELETE ){
                exeDestroy( destination.removeItemAt( _mapVal2LstIndex[event.oldValue] ) );
                delete _mapVal2LstIndex[event.oldValue];
            }

            CONFIG::debugging{
                if( _source.length != _destination.length ){
                    throw new Error( UIDUtil.getUID( this ) + ".onSrcChange error _source.length != _destination.length" );
                }//debug
            }
            setSynchronizingChangeChanged( false );
        }

        private var _syncInitializeStatus:uint = 0;
        public function get syncInitializeStatus():uint{
            return _syncInitializeStatus;
        }

        protected function initializeSync():void{
            if( _source == null || _destination == null || _getDestInst == null || ! _enabled ){
                _syncInitializeStatus = 0;
                return;
            }
            CONFIG::debugging{
                if( _printLog ){
                    CC.log( getUIDs() + ":::initializeSync()ing" );
                }
                if( _synchronizingChange ){
                    throw new Error( getUIDs() + ".initializeSync _synchronizingChange is true!!!" );
                }
            }//debug
            _source.removeEventListener( PropertyChangeEvent.PROPERTY_CHANGE, onSrcPropertyChanged );
            _syncInitializeStatus = 1;
            _mapVal2LstIndex = new Dictionary();
            while( destination.length > 0 ){
                exeDestroy( destination.removeItemAt( 0 ) );
            }

            const len:int = source.length;
            for( var j:int = 0; j < len; j ++ ){
                var valueAddingToList:* = exeGetDestInst( source.keys[j] );
                var index:uint = exeCompare( valueAddingToList );
                _mapVal2LstIndex[valueAddingToList] = index;
                destination.addItemAt( valueAddingToList, index );
            }

            CONFIG::debugging{
                if( _source.length != _destination.length ){
                    throw new Error( getUIDs() + ".initializeSync error src.len != dest.len !!!!" )
                }
                if( _printLog ){
                    CC.log( getUIDs() + ":::initializeSync()ed" );
                }
            }//debug

            _source.addEventListener( PropertyChangeEvent.PROPERTY_CHANGE, onSrcPropertyChanged, false, priority );
            _syncInitializeStatus = 2;
        }

        private var _mapVal2LstIndex:Dictionary = new Dictionary();

        public function getDestValueBySourceValue( srcValue:Object ):Object{
            var index:int = _mapVal2LstIndex[srcValue];
            if( index < 0 ){
                return null;
            }
            return _destination.getItemAt( index );
        }

        public function destroy( callDestDestroy:Boolean = false ):void{
            if( callDestDestroy ){
                for( var j:int = _destination.length - 1; j >= 0; j -- ){
                    exeDestroy( _destination.getItemAt( j ) );
                }
            }
            CONFIG::debugging{
                delete dest2sync[_destination];
            }

            _source = null;
            _destination = null;
            _getDestInst = null;
        }

        CONFIG::debugging{
            private var _uids:String = null;
            private var _printLog:Boolean = false;

            public function getUIDs():String{
                _uids = UIDUtil.getUID( this ) + "::" + UIDUtil.getUID( _source ) + "::" + UIDUtil.getUID( _destination );
                return _uids;
            }
        }//debug
        ]]></mx:Script>
</utils:MxmlEventDispatcher>
